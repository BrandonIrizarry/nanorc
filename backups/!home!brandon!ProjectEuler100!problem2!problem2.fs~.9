\ Problem 2
\ 
\ Each new term in the Fibonacci sequence is generated by adding the previous two 
\ terms. By starting with 1 and 2, the first 10 terms will be: 1, 2, 3, 5, 8, 13, 
\ 21, 34, 55, 89, ... By considering the terms in the Fibonacci sequence whose 
\ values do not exceed four million, find the sum of the even-valued terms.

\ Even-valued Fibonacci numbers occur every third iteration of the loop.
\ The loop used is pseudo-infinite on purpose, since I only need the
\ current loop index available to me readily, without having to 
\ work it out on the stack myself.

: fibnext ( a b -- b a+b)
	swap over + ;

: main ( --)
	0 1 2 0 0 do
		i 3 mod 0= if
			rot over + -rot
		then fibnext

		dup 4000000 > if leave then
	loop 2drop ;

main . \ 4613732
